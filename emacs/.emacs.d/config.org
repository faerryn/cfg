#+TITLE: Faerryn's Emacs Config
#+STARTUP: indent overview

Welcome to my humble parish of the Church of Emacs.
* Prologue
This section contains essential components necessary for the rest of
my config to work.
** Lexical Binding
[[info:elisp#Lexical Binding][Lexical Binding]] encapsulates the scoping of variables to their
context.  This can make bugs easier to catch and improve performance.
#+begin_src emacs-lisp
  ;;; config.el -*- lexical-binding: t; -*-
#+end_src
** Package Manager
Straight.el is a declarative package manager for Emacs.
#+begin_src emacs-lisp
  (custom-set-variables
   '(straight-check-for-modifications nil)
   '(straight-use-symlinks t)
   '(straight-vc-git-default-clone-depth 1))
  (let ((dest (expand-file-name "straight/repos/straight.el/" user-emacs-directory))
        (repo "https://github.com/raxod502/straight.el.git"))
    (unless (file-directory-p dest)
      (call-process "git" nil nil nil
                    "clone"
                    "--depth" "1"
                    repo
                    dest))
    (require 'straight (expand-file-name "straight.el" dest))
    (with-temp-buffer
      (insert-file-contents (expand-file-name "bootstrap.el" dest))
      (eval-region (search-forward "(require 'straight)")
                   (point-max))))
#+end_src
** Garbage Collector
GCMH is an intelligent garbage collection scheduler for Emacs.
#+begin_src emacs-lisp
  (straight-use-package 'gcmh)
  (add-hook 'emacs-startup-hook #'gcmh-mode)
#+end_src
** Custom Symbols
A hook that runs before the minibuffer opens.
#+begin_src emacs-lisp
  (defvar faerryn/before-minibuffer-hook nil
    "Normal hook run just before entry to minibuffer")
  (defun faerryn/run-before-minibuffer-hooks (&rest ignored)
    (run-hooks 'faerryn/before-minibuffer-hook))
  (advice-add #'read-from-minibuffer :before #'faerryn/run-before-minibuffer-hooks)
  (advice-add #'read-no-blanks-input :before #'faerryn/run-before-minibuffer-hooks)
  (advice-add #'read-string :before #'faerryn/run-before-minibuffer-hooks)
#+end_src
* User Interface
Blinging out Emacs since 2021.
** Declutter Widgets
Decluttering Emacs of frivolous things like menus and scroll bars.
#+begin_src emacs-lisp
  (custom-set-variables
   '(inhibit-startup-screen t)
   '(use-dialog-box nil)
   '(menu-bar-mode nil)
   '(tool-bar-mode nil)
   '(scroll-bar-mode nil)
   '(tooltip-mode nil))
#+end_src
** Line Numbers
Line numbers are crucial to understand where you are in a document.
#+begin_src emacs-lisp
  (custom-set-variables
   '(global-display-line-numbers-mode t)
   '(display-line-numbers-type t))
#+end_src
** Minibuffer Completion
Emacs prompts for input through the minibuffer, which often includes
tab completion and other useful features.
*** Consult
Consult provides a collection of useful functions that make use of minibuffer completion.
#+begin_src emacs-lisp
  (straight-use-package 'consult)
  (setq completion-in-region-function #'consult-completion-in-region)
#+end_src
*** Marginalia
Marginalia provides extra information in the "margins" during minibuffer completion.
#+begin_src emacs-lisp
  (straight-use-package 'marginalia)
  (marginalia-mode +1)
#+end_src
*** Vertico
Vertico provices a performant and extensible minibuffer-based completion framework.
#+begin_src emacs-lisp
  (straight-use-package
   `(vertico :files ,(cons "extensions/*.el" straight-default-files-directive)))
  (vertico-mode +1)
#+end_src

This enables mouse support.
#+begin_src emacs-lisp
  (vertico-mouse-mode +1)
#+end_src
** Modeline
The modeline is the the bar the displays, among other things, a
buffer's name and major mode.

Emacs' default modeline is weird.  Doom Emacs provides us with an
aesthetic alternative.
#+begin_src emacs-lisp
  (straight-use-package 'doom-modeline)
  (custom-set-variables
   '(doom-modeline-icon nil))
  (add-hook 'emacs-startup-hook #'doom-modeline-mode)
#+end_src

Show the time and battery level on the modeline.
#+begin_src emacs-lisp
  (custom-set-variables
   '(display-time-mode t)
   '(display-time-default-load-average nil)
   '(display-time-format "%r")
   '(display-time-interval 1)
   '(display-battery-mode t)
   '(battery-mode-line-format "%p%%")
   '(battery-update-interval 1))
#+end_src
** Rainbow
Paint the background of color codes with the color they represent upon
calling `rainbow-mode'.
#+begin_src emacs-lisp
  (straight-use-package 'rainbow-mode)
#+end_src
** Show Paren
This highlights matching quotes, parentheses, brackets, etc...
#+begin_src emacs-lisp
  (custom-set-variables
   '(show-paren-mode t))
#+end_src
** Silent Bell
Loud bells annoy me.
#+begin_src emacs-lisp
  (custom-set-variables
   '(visible-bell t))
#+end_src
** Tab Bar
The tab bar manages different buffers and window configuration in
different tabs.  This feature seems stolen from Vim.
#+begin_src emacs-lisp
  (custom-set-variables
   '(tab-bar-mode t)
   '(tab-bar-close-button nil)
   '(tab-bar-new-button nil))
#+end_src
** Theme
Emacs' default theme is blindingly bright.  Doom Emacs (once again)
provides an aesthetic alternative.
#+begin_src emacs-lisp
  (straight-use-package 'doom-themes)
  (load-theme 'doom-one t)
#+end_src
** Truncate Lines
Line wrap is weird and confusing.  Truncated lines are elephants.
#+begin_src emacs-lisp
  (custom-set-variables
   '(truncate-lines t))
#+end_src
** Whitespace
Display normally transparent characters.  Catch the trailing spaces.
#+begin_src emacs-lisp
  (custom-set-variables
   '(global-whitespace-mode t)
   '(whitespace-style '(face tabs trailing tab-mark)))
#+end_src
* Text Editing
Emacs is a text-editor, after all.
** Custom Keys
Key bindings are the bread and butter of Emacs.  It goes to reason
that you can define your own!
#+begin_src emacs-lisp
  (define-key (current-global-map) (kbd "C-c x") #'fixup-whitespace)
#+end_src
** Electric Pairs
Automagically close quotes, parentheses, brackets, etc...
#+begin_src emacs-lisp
  (custom-set-variables
   '(electric-pair-mode t))
#+end_src
** Indentation
Indent with 2 spaces.
#+begin_src emacs-lisp
  (custom-set-variables
   '(indent-tabs-mode nil))
#+end_src
** LSP Client
[[https://microsoft.github.io/language-server-protocol][Language server protocol]] is a protocol invovling a server generating
intelligent code assistance for an abitrary editor that supports the
protocol.  It brings IDE-grade completion and linting to any
LSP-capable editor.

Eglot is a LSP client written in Elisp, bringing the power of an IDE
to Emacs.
#+begin_src emacs-lisp
  (straight-use-package 'eglot)
  (with-eval-after-load "eglot"
    (define-key eglot-mode-map (kbd "C-c a") #'eglot-code-actions)
    (define-key eglot-mode-map (kbd "C-c r") #'eglot-rename)
    (define-key eglot-mode-map (kbd "C-c f") #'eglot-format))
#+end_src

This sets up format-on-save.
#+begin_src emacs-lisp
  (defun faerryn/eglot-setup-fosa ()
    (if (eglot-managed-p)
        (add-hook 'before-save-hook #'eglot-format nil t)
      (remove-hook 'before-save-hook #'eglot-format t)))
  (add-hook 'eglot-managed-mode-hook #'faerryn/eglot-setup-fosa)
#+end_src
** Tab Completion
Bring the tab completion to Emacs!
#+begin_src emacs-lisp
  (custom-set-variables
   '(tab-always-indent 'complete))
#+end_src
* Major Modes
Major modes represent filetypes in Emacs.
** C/C++
Bringing tab-to-complete to C/C++, which is useful when combined with
LSP servers that provide completion.
#+begin_src emacs-lisp
  (with-eval-after-load "cc-mode"
    (advice-add 'c-indent-command :override #'indent-for-tab-command))
#+end_src
** Dired
Dired is the file browser in Emacs.

This prettifies dired's file listing.
#+begin_src emacs-lisp
  (custom-set-variables
   '(dired-listing-switches "-AFghvX --group-directories-first")
   '(dired-guess-shell-alist-user
     '(("\\.swf\\'" "ruffle")
       ("\\.pdf\\'" "zathura")
       ("\\.mp4\\'" "mpv")
       ("\\.ogg\\'" "mpv"))))
#+end_src

This loads up extra features for dired.
#+begin_src emacs-lisp
  (custom-set-variables
   '(dired-x-hands-off-my-keys nil))
  (require 'dired-x)
#+end_src
** Lisp Interaction
It's watch *scratch* buffers are made of!
#+begin_src emacs-lisp
  (autoload 'orgtbl-mode "org-table"
    "The Org mode table editor as a minor mode for use in other modes." t)
  (add-hook 'lisp-interaction-mode-hook #'orgtbl-mode)
#+end_src
** Markdown
#+begin_src emacs-lisp
  (straight-use-package 'markdown-mode)
#+end_src
** OCaml
OCaml is a funny programming language.
#+begin_src emacs-lisp
  (straight-use-package 'caml-mode)
  (autoload 'caml-mode "caml"
    "Major mode for editing OCaml code." t)
  (add-to-list 'auto-mode-alist '("\\.ml[iylp]?$" . caml-mode))
#+end_src
** Org
Emacs' Org mode has superpowers.
#+begin_src emacs-lisp
  (custom-set-variables
   '(org-catch-invisible-edits 'smart))
#+end_src
** Pdf
#+begin_src emacs-lisp
  (straight-use-package 'pdf-tools)
  (autoload 'pdf-view-mode "pdf-view")
  (add-to-list 'auto-mode-alist '("\\.[pP][dD][fF]\\'" . pdf-view-mode))
#+end_src
** Rust
[[https://www.rust-lang.org][Rust]] is a programming language with focus on performance and memory
safety.  It boasts a builtin project and dependency manager, and
delightfully concise syntax.
#+begin_src emacs-lisp
  (straight-use-package 'rust-mode)
#+end_src

This sets up rust-analyzer as the default lsp server for rust-mode.
#+begin_src emacs-lisp
  (with-eval-after-load "eglot"
    (add-to-list 'eglot-server-programs '(rust-mode . ("rust-analyzer"))))
#+end_src
** Zig
[[https://ziglang.org][Zig]] is an ambitious programming language with zero cost duck-typing
and concise code.  It boasts an incredibly powerful type system and
featureful compile-time code execution.
#+begin_src emacs-lisp
  (straight-use-package 'zig-mode)
  (custom-set-variables
   '(zig-format-on-save nil))
#+end_src
* Xorg Integration
Xorg is the classic Linux display server.
** Terminal Emulator
Emacs can run in a text terminal through `emacs -nw'.

Text terminals sometimes support the mouse, and emacs can support text
terminals supporting the mouse.
#+begin_src emacs-lisp
  (add-hook 'tty-setup-hook #'xterm-mouse-mode)
#+end_src

Emacs in an X11 terminal emulator cannot access the clipboard by
default.  This package detects a suitable CLI clipboard tool (such as
the eponymous `xclip') to integrate into Emacs.
#+begin_src emacs-lisp
  (straight-use-package 'xclip)
  (defun faerryn/safe-xclip-mode ()
    (ignore-errors (xclip-mode +1)))
  (add-hook 'tty-setup-hook #'faerryn/safe-xclip-mode)
#+end_src
** Window Manager
An X11 window manager is what powers most Linux desktops.

EXWM is a window manager written in Elisp.  Lots of hacks are used to
customize the behavior of EXWM.
#+begin_src emacs-lisp
  (straight-use-package 'exwm)
#+end_src

This defines key behaviors for EXWM.
#+begin_src emacs-lisp
  (with-eval-after-load "exwm"
    (define-key exwm-mode-map (kbd "C-c f") #'exwm-layout-toggle-fullscreen)
    (define-key exwm-mode-map (kbd "C-q") #'exwm-input-send-next-key))
  (custom-set-variables
   '(exwm-input-prefix-keys (mapcar #'kbd '("C-c" "C-g" "C-h" "C-u" "C-x" "M-!" "M-&" "M-:" "M-x"))))
#+end_src

This gets rid of EXWM's buffer-predicate settings.
#+begin_src emacs-lisp
  (defun faerryn/exwm-workspace-init-fix (oldfun)
    (advice-add #'modify-all-frames-parameters :override #'ignore)
    (funcall oldfun)
    (advice-remove #'modify-all-frames-parameters #'ignore))
  (advice-add #'exwm-workspace--init :around #'faerryn/exwm-workspace-init-fix)
  (defun faerryn/mfp-ignore-bp (oldfun frame alist)
    (funcall oldfun frame (assoc-delete-all 'buffer-predicate alist)))
  (defun faerryn/exwm-workspace-exit-fix (oldfun)
    (advice-add #'modify-frame-parameters :around #'faerryn/mfp-ignore-bp)
    (funcall oldfun)
    (advice-remove #'modify-frame-parameters #'faerryn/mfp-ignore-bp))
  (advice-add #'exwm-workspace--exit :around #'faerryn/exwm-workspace-exit-fix)
#+end_src

This names exwm buffers according to the X11 window class.
#+begin_src emacs-lisp
  (defun faerryn/exwm-rename-buffer ()
    (rename-buffer (concat "*EXWM " exwm-class-name "*") t))
  (add-hook 'exwm-update-class-hook #'faerryn/exwm-rename-buffer)
#+end_src

This makes EXWM fullscreen to stay in line mode.
#+begin_src emacs-lisp
  (advice-add #'exwm-layout-set-fullscreen :after #'exwm-input-grab-keyboard)
#+end_src

This makes EXWM exit fullscreen whenever another buffer gains focus.
#+begin_src emacs-lisp
  (defun faerryn/exwm-unfullscreen ()
    (dolist (buf (buffer-list))
      (when-let ((id (exwm--buffer->id buf)))
        (with-current-buffer buf
          (exwm-layout-unset-fullscreen id)))))
  (defun faerryn/exwm-switch-to-buffer (&rest r)
    (interactive (lambda (oldfunspec)
                   (faerryn/exwm-unfullscreen)
                   (advice-eval-interactive-spec oldfunspec)))
    (faerryn/exwm-unfullscreen))
  (defun faerryn/exwm-init-hook ()
    (add-hook 'faerryn/before-minibuffer-hook #'faerryn/exwm-unfullscreen)
    (add-hook 'window-state-change-hook #'faerryn/exwm-unfullscreen)
    (advice-add #'switch-to-buffer :before #'faerryn/exwm-switch-to-buffer))
  (add-hook 'exwm-init-hook #'faerryn/exwm-init-hook)
  (defun faerryn/exwm-exit-hook ()
    (remove-hook 'faerryn/before-minibuffer-hook #'faerryn/exwm-unfullscreen)
    (remove-hook 'window-state-change-hook #'faerryn/exwm-unfullscreen)
    (advice-remove #'switch-to-buffer #'faerryn/exwm-switch-to-buffer))
  (add-hook 'exwm-exit-hook #'faerryn/exwm-exit-hook)
#+end_src
* Sundry
Miscellaneous items that don't fall into a broader category.
** Async Shell Command
Configure the behavior of [[help:async-shell-command]]
#+begin_src emacs-lisp
  (custom-set-variables
   '(async-shell-command-buffer #'rename-buffer))
  (add-to-list 'display-buffer-alist
               '("\\`\\*Async Shell Command\\*"
                 (display-buffer-no-window)))
#+end_src
** Compiler Explorer
RMSBolt is a compiler explorer inspired by [[https://godbolt.org][Godbolt]].  It allows a
programmer to see how individual lines of code are compiled.
#+begin_src emacs-lisp
  (straight-use-package 'rmsbolt)
  (custom-set-variables
   '(rmsbolt-automatic-recompile nil))
#+end_src

This sets up rmsbolt on activation of the minor mode and on save.
#+begin_src emacs-lisp
  (defun faerryn/rmsbolt-setup-cosa ()
    (unless (derived-mode-p 'asm-mode)
      (if rmsbolt-mode
          (progn
            (unless (buffer-modified-p)
              (rmsbolt-compile))
            (add-hook 'after-save-hook #'rmsbolt-compile nil t))
        (remove-hook 'after-save-hook #'rmsbolt-compile t))))
  (add-hook 'rmsbolt-mode-hook #'faerryn/rmsbolt-setup-cosa)
#+end_src
** Declutter File System
Disable autosaves, backups, and lockfiles for file system sanity.
#+begin_src emacs-lisp
  (custom-set-variables
   '(auto-save-default nil)
   '(make-backup-files nil)
   '(create-lockfiles nil))
#+end_src
** EMMS
#+begin_src emacs-lisp
  (straight-use-package 'emms)
  (custom-set-variables
   '(emms-source-file-default-directory "~/Music/"))
  (with-eval-after-load "emms"
    (emms-all)
    (emms-default-players))
  (autoload 'emms "emms-playlist-mode" "Switch to the current emms-playlist buffer." t)
#+end_src
** Eshell
Eshell is a portable, lispy shell in Emacs.  This configures aliases
for eshell.
#+begin_src emacs-lisp
  (with-eval-after-load "em-alias"
    (eshell/alias "ls" "ls -FhvX --color=auto --group-directories-first $*")
    (eshell/alias "ll" "ls -g $*")
    (eshell/alias "la" "ll -A $*"))
#+end_src
** Follow Symlinks
For symlinks are good.
#+begin_src emacs-lisp
  (custom-set-variables
   '(find-file-visit-truename t)
   '(vc-follow-symlinks t))
#+end_src
** Git Client
Magit is a very nice git client for Emacs.
#+begin_src emacs-lisp
  (straight-use-package 'magit)
  (define-key (current-global-map) (kbd "C-x g") #'magit-status)
#+end_src
** Tramp
Tramp is how Emacs can access remote files as if they were local.

Let Tramp inherit the remote's PATH environmental variable.
#+begin_src emacs-lisp
  (with-eval-after-load "tramp"
    (add-to-list 'tramp-remote-path 'tramp-own-remote-path))
#+end_src
** Vterm
#+begin_src emacs-lisp
  (straight-use-package 'vterm)
#+end_src
